buildscript {
    repositories {
        maven { url "https://repository.exoplatform.org/public/" }
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.google.gms:google-services:4.3.3'
    }
}

allprojects {
    buildscript {
        repositories {
            maven { url "https://repository.exoplatform.org/public/" }
        }
    }
    repositories {
        maven { url "https://repository.exoplatform.org/public/" }
        mavenLocal()
        google()
    }

    afterEvaluate { project ->
        // Load signing configuration
        def configName = 'release'

        if (project.hasProperty("android") && android.signingConfigs.hasProperty(configName)) {
            def propsFileEnv = System.getenv('GOOGLE_PLAY_EXO_KEYSTORE_PROPS')
            if (propsFileEnv != null){
                def propsFile = file(propsFileEnv)
                if (propsFile.exists()) {
                    println "Loading signing config for $project from " + propsFile
                    def props = new Properties()
                    props.load(new FileInputStream(propsFile))
                    android.signingConfigs[configName].storeFile = file(props['storeFile'])
                    android.signingConfigs[configName].storePassword = props['storePassword']
                    android.signingConfigs[configName].keyAlias = props['keyAlias']
                    android.signingConfigs[configName].keyPassword = props['keyPassword']
                } else {
                    println "The property file referenced by GOOGLE_PLAY_EXO_KEYSTORE_PROPS environment variable doesn't exists or is not readable !"
                        +" no signing configuration for release will be configured. (GOOGLE_PLAY_EXO_KEYSTORE_PROPS: "+propsFileEnv+")"
                }
            } else {
                println "GOOGLE_PLAY_EXO_KEYSTORE_PROPS environment variable doesn't exists ! no signing configuration for release will be configured."
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// This block encapsulate custom properties and make them available to all modules in the project.
ext {
    minSdkVersion = 19 // Android 4.4 (KitKat)
    compileSdkVersion = 29
    supportLibVersion = "28"
}